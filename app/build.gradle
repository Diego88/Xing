import com.hiberus.mobile.android.AppConfig
import com.hiberus.mobile.android.dependencies.Dependencies
import com.hiberus.mobile.android.dependencies.Modules
import com.hiberus.mobile.android.dependencies.Plugins

apply plugin: Plugins.androidApplication
apply plugin: Plugins.firebaseCrashlytics
apply from: "$rootDir/base_kotlin_android.gradle"
apply plugin: Plugins.navigationSafeArgsKotlin

android {
    compileSdkVersion AppConfig.compileSdkVersion
    buildToolsVersion AppConfig.buildToolsVersion

    defaultConfig {
        applicationId AppConfig.applicationId
        minSdkVersion AppConfig.minSdkVersion
        targetSdkVersion AppConfig.targetSdkVersion
        versionCode AppConfig.versionCode
        versionName AppConfig.versionName

        testInstrumentationRunner AppConfig.testInstrumentationRunner
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }

    packagingOptions {
        exclude "**/attach_hotspot_windows.dll"
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/licenses/ASM'
    }
}

dependencies {

    // Modules
    implementation project(Modules.model)
    implementation project(Modules.dataLocal)
    implementation project(Modules.dataRemote)
    implementation project(Modules.dataSession)
    implementation project(Modules.dataRepository)
    implementation project(Modules.domain)
    androidTestImplementation project(Modules.commonTest)

    // Android Tools
    implementation Dependencies.AndroidTools.desugaring

    // Kotlin
    implementation Dependencies.Kotlin.stdLib
    implementation Dependencies.Kotlin.ktxCore

    // AndroidX
    implementation Dependencies.AndroidX.appcompat
    implementation Dependencies.AndroidX.constraintLayout
    implementation Dependencies.AndroidX.material
    implementation Dependencies.AndroidX.navigation
    implementation Dependencies.AndroidX.navigationFragment
    implementation Dependencies.AndroidX.support

    // Lifecycle
    implementation Dependencies.AndroidX.lifecycleViewModel
    implementation Dependencies.AndroidX.lifecycleLiveData

    // Koin
    implementation Dependencies.Koin.koinCompiler
    implementation Dependencies.Koin.koin
    implementation Dependencies.Koin.koinViewModel

    // Glide
    implementation Dependencies.Glide.glide

    // Unit Test
    testImplementation Dependencies.Test.koinTestJunit
    testImplementation Dependencies.Test.mockitoKotlin
    testImplementation Dependencies.Test.mockitoInline

    // Instrumentation Test
    debugImplementation Dependencies.AndroidTest.fragmentTesting
    androidTestImplementation Dependencies.Test.junit
    androidTestImplementation Dependencies.Test.coroutinesTest
    androidTestImplementation Dependencies.AndroidTest.archCoreTest
    androidTestImplementation Dependencies.AndroidTest.core
    androidTestImplementation Dependencies.AndroidTest.testCoreKtx
    androidTestImplementation(Dependencies.Test.koinTest) {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }
    androidTestImplementation(Dependencies.Test.mockitoKotlin) {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }
    androidTestImplementation Dependencies.Test.mockitoAndroid
    androidTestImplementation Dependencies.AndroidTest.androidJunit
    androidTestImplementation Dependencies.AndroidTest.androidJunitKtx
    androidTestImplementation Dependencies.AndroidTest.espressoCore
    androidTestImplementation Dependencies.AndroidTest.espressoIntents
    androidTestImplementation Dependencies.AndroidTest.espressoContrib
    androidTestImplementation Dependencies.AndroidTest.runner
    androidTestImplementation Dependencies.AndroidTest.rules
    androidTestImplementation(Dependencies.AndroidTest.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(Dependencies.AndroidTest.espressoIntents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    // Exclude Material Design related libraries
    androidTestImplementation(Dependencies.AndroidTest.espressoContrib) {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
        exclude module: 'material'
    }
    androidTestImplementation(Dependencies.AndroidTest.runner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(Dependencies.AndroidTest.rules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Google
    implementation Dependencies.Firebase.firebaseCrashlytics

    // Developer Tools
    implementation Dependencies.DeveloperTools.timber
    debugImplementation Dependencies.DeveloperTools.leakCanary
    debugImplementation Dependencies.DeveloperTools.stetho
}